# プレイヤーデータ同期スクリプト
# 必須アドオン: Skript-DB

# プレイヤー参加時: DBから所持金をロード
on join:
    # 少し待ってから実行（他のプラグインとの競合回避）
    wait 1 second
    
    set {_uuid} to "%uuid of player%"
    set {_name} to name of player
    
    send "&a[System] %player% joined with UUID: %{_uuid}%" to console
    
    # DBから参照
    execute "SELECT money FROM player_information WHERE player_uuid = %{_uuid}% LIMIT 1" in {kenkoku_db} and store the result in {-result::*}
    
    if {-result::money::1} is set:
        # DBにデータがある場合 -> ロード
        set metadata "money" of player to {-result::money::1}
        # OOM対策: DB結果変数を即座にクリア
        delete {-result::*}
        # 名前が変わっている可能性があるため更新しておく
        execute "UPDATE player_information SET player_name = %{_name}%, updated_at = NOW() WHERE player_uuid = %{_uuid}%" in {kenkoku_db}
        set {_err} to last sql error
        if {_err} is set:
            send "&c[System] UPDATE error for %player%: %{_err}%" to console
        
        set {_m} to metadata "money" of player
        send "&a[System] 所持金をロードしました: %{_m}%G" to console
    else:
        # OOM対策: DB結果変数を即座にクリア
        delete {-result::*}
        # DBにデータがない場合 -> 新規作成 (0円)
        set metadata "money" of player to 0
        execute "INSERT INTO player_information (player_uuid, player_name, money, created_at, updated_at) VALUES (%{_uuid}%, %{_name}%, 0, NOW(), NOW())" in {kenkoku_db}
        set {_err} to last sql error
        if {_err} is set:
            send "&c[System] INSERT error for %player%: %{_err}%" to console
        else:
            send "&a[System] 新規プレイヤーデータを登録しました。" to console

# プレイヤー退出時: 念のため保存（基本は即時保存だが保険として）
on quit:
    set {_uuid} to "%uuid of player%"
    set {_money} to metadata "money" of player
    if {_money} is not set:
        set {_money} to 0
        
    execute "UPDATE player_information SET money = %{_money}%, updated_at = NOW() WHERE player_uuid = %{_uuid}%" in {kenkoku_db}
    set {_err} to last sql error
    if {_err} is set:
        send "&c[System] QUIT UPDATE error for %player%: %{_err}%" to console
    else:
        send "&a[System] %player% quit, saved money: %{_money}%G" to console

